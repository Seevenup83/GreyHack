// NAME: 7wifi (connects automatic to the next WiFi found)
cryptools = include_lib("/lib/crypto.so")
if not cryptools then exit("Missing cryptools: /lib/crypto.so")

// AIRMON
computer = get_shell.host_computer
networkDevices = computer.network_devices

counter = 0
interfaceName = ""
while networkDevices[counter] != " "
  interfaceName = interfaceName + networkDevices[counter]
  counter = counter + 1
end while

output = cryptools.airmon("start", interfaceName)
if not output then exit("airmon: " + interfaceName + " not found")
if typeof(output) == "string" then error(output)
print("airmon started on: " + interfaceName)

// WIFI NETWORKS
wifiNetworks = computer.wifi_networks(interfaceName)

counter = 0
wifiStrong = 0
print("searching for strongest WiFi-Network ...")
for wifiNetwork in wifiNetworks
	wifiConnection = wifiNetworks[counter].split(" ") //TBD: check if last is always the strongest
	wifiPower = wifiConnection[1].split("%")[0]
	
	if wifiPower > wifiStrong then
	  wifiStrong = wifiPower
	  bssid = wifiConnection[0]
      essid = wifiConnection[2]
	end if
	counter = counter + 1
end for

// AIRPLAY
print("aireplay using WiFi: " + essid)
if wifiStrong >= 75 then
  maxAcks = 3000
else if wifiStrong >= 50 then
  maxAcks = 4000
else
  maxAcks = 20000
end if

print("WiFi connection power is: " + wifiStrong + " setting MaxAcks to: " + maxAcks)

result = cryptools.aireplay(bssid, essid, maxAcks)
if typeof(result) == "string" then exit(result)

// AIRCRACK
pathFile = "file.cap"
file = computer.File(pathFile)
if file == null then exit("aircrack: file not found: " + pathFile)
if not file.is_binary then exit("aircrack: not valid filecap.")		
if not file.has_permission("r") then exit("aircrack: permission denied")

password = cryptools.aircrack(file.path)
file.delete // Cleanup: remove file.cap
if not password then exit("aircrack: Unable to get the key")

if computer.connect_wifi(interfaceName, bssid, essid, password) then
  print("successfully connected to WiFi: " + essid + " with password: " + password)
else
  exit("could not connect to WiFi: " + essid)
end if
